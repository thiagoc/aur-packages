# Maintainer: thiagoc <root@thiagoc.net>
# https://github.com/thiagoc/aur-packages

pkgname=atraci-bin
pkgver=0.6.5
pkgrel=2
pkgdesc='An application that lets you listen instantly to more than 60 million songs'
arch=('i686' 'x86_64')
url='http://getatraci.net'
license=('MIT')
depends=('gconf' 'libnotify')
optdepends=('libudev.so.0: install it if Atraci does not run correctly')
conflicts=('atraci-git' 'atraci')
options=('!strip')
_pkgarch='32'
_md5sum='bd2fc957d7705b79d481bc49091c3794'
if [ "${CARCH}" == 'x86_64' ]; then
  _pkgarch='64'
  _md5sum='ef288b5f1ad34905d8ce6c7f42406166'
fi
source=(https://github.com/Atraci/Atraci/releases/download/${pkgver}/Atraci-linux${_pkgarch}.tgz
        atraci.desktop
        atraci.install
        atraci.png)
md5sums=($_md5sum
         'abb9ee963a310dde90b24250163d1e89'
         '57a030ed14fb00d3df2a74bc499680e9'
         'ca605a2dfdec42b086201731c7315daa')

package() {
  cd ${srcdir}
  install -dm 755 ${pkgdir}/opt/${pkgname}
  install -dm 755 ${pkgdir}/usr/bin
  install -Dm 755 linux${_pkgarch}/Atraci ${pkgdir}/opt/${pkgname}
  install -Dm 644 linux${_pkgarch}/libffmpegsumo.so ${pkgdir}/opt/${pkgname}
  install -Dm 644 linux${_pkgarch}/nw.pak ${pkgdir}/opt/${pkgname}
  install -Dm 644 linux${_pkgarch}/icudtl.dat ${pkgdir}/opt/${pkgname}
  install -Dm 644 atraci.desktop ${pkgdir}/usr/share/applications/atraci.desktop
  install -Dm 644 atraci.png ${pkgdir}/usr/share/pixmaps/atraci.png
  ln -s /opt/${pkgname}/Atraci ${pkgdir}/usr/bin/atraci
}

# vim:set ts=2 sw=2 et:
